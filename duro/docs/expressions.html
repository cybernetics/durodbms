<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
"http://www.w3.org/TR/html4/strict.dtd">
<html lang="en">
<head>
<title>DuroDBMS expression syntax</title>
<link rel="stylesheet" href="style.css">
<body>
<p><a href="index.html">Documentation home</a>

<h2>DuroDBMS expression syntax</h2>

<p>The language for DuroDBMS expressions is a subset of Tutorial D, as defined
in <cite>TTM</cite>, third edition and <cite>Database Explorations</cite>,
with a number of differences. The most important are:

<ol>
<li>DuroDBMS uses a dot ('.') operator for tuple attribute extraction, in addition to FROM.
<li>DuroDBMS supports LIKE and REGEX_LIKE operators for pattern matching.
<li>DuroDBMS uses the keyword SUBSET_OF for the subset operator
and IN for the element-of operator.
<li>DuroDBMS supports both single ("'") and double quotes ('"') as delimiters in string literals.
<li>GROUP and UNGROUP only support single grouping and ungrouping, as in
the second edition of TTM.
</ol>

<h3>DuroDBMS expression syntax (EBNF notation)</h3>

<pre>
expression = project | where | rename | dyadic_relation_op_inv 
        | extend | update | summarize | divideby
	| wrap | unwrap | group | ungroup
	| expression OR expression
	| expression AND expression
	| expression XOR expression
	| NOT expression
	| rel_expression
	| '+' expression | '-' expression
        | expression '+' expression
        | expression '-' expression
        | expression '||' expression
        | expression '*' expression
        | expression '/' expression
	| var_expression
	| literal
	| count_inv
	| aggregate_inv
	| '(' expression ')'
	| expression '.' id
	| id FROM expression
	| TUPLE FROM expression
	| ifthenelse | with.

project = expression '{' [ALL BUT] [id {',' id}] '}'.

where = expression WHERE expression.

rename = expression RENAME '(' [id AS id {',' id AS id}] ')'.

dyadic_relation_op_inv = expression UNION expression
        | expression INTERSECT expression
        | expression MINUS expression
        | expression SEMIMINUS expression
        | expression JOIN expression
        | expression SEMIJOIN expression
        | expression MATCHING expression
        | expression NOT MATCHING expression.

extend = EXTEND expression ':' '{' [id ':=' expression {',' id ':=' expression}] '}'.

update = UPDATE expression ':' '{' id ':=' expression {',' id ':=' expression} '}'.

summarize = SUMMARIZE expression PER expression
           ADD '(' [summary AS id {',' summary AS id}] ')'.

divideby = expression DIVIDEBY expression PER expression.

summary = COUNT '(' ')' | summary_type '(' expression ')'.

summary_type = SUM | AVG | MAX | MIN | ALL | AND | ANY | OR.

wrap = expression WRAP '(' [wrapping {',' wrapping}] ')'.

wrapping = '{' [id {',' id}] '}' AS id.

unwrap = expression UNWRAP '(' [id {',' id}] ')'.

group  = expression GROUP '{' [id {',' id}] '}' AS id.

ungroup = expression UNGROUP id.

var_expression = id | op_inv | var_expression '[' expression ']'.

with = WITH '(' [id ':=' expression {',' id ':=' expression}] ')' ':' expression.

rel_expression = expression | expression '=' expression
        | expression '&lt;>' expression
        | expression '!=' expression
        | expression '>=' expression
        | expression '&lt;=' expression
        | expression '>' expression
        | expression '&lt;' expression
        | expression SUBSET_OF expression
        | expression IN expression
        | expression LIKE expression
        | expression REGEX_LIKE expression.

ifthenelse = IF expression THEN expression ELSE expression.

count_inv = COUNT '(' expression ')'.

aggregate_inv = summary_type '(' ne_expression_list ')'.

op_inv = id '(' expression_list ')'.

literal = string | integer | decimal | float | TRUE | FALSE
        | RELATION '{' id_type_list '}' '{' expression_list '}'
        | RELATION '{' ne_expression_list '}'
        | TABLE_DEE | TABLE_DUM
        | TUPLE '{' tuple_item_list '}'
        | ARRAY type '{' expression_list '}'
        | ARRAY '{' ne_expression_list '}'

tuple_item_list = [id expression {',' id expression}].

expression_list = [ne_expression_list].

ne_expression_list = expression {',' expression}.

type = id | TUPLE '{' id_type_list '}' | RELATION '{' id_type_list '}'
        | ARRAY type | SAME_TYPE_AS '(' expression ')'
        | TUPLE SAME_HEADING_AS '(' expression ')'
        | RELATION SAME_HEADING_AS '(' expression ')'.

id_type_list = [id type {',' id type}].

string = '"'{esc_char}'"' | "'"{character}"'".

esc_char = character | '\"' | '\\' | '\n' | '\r' | '\t'.

integer = digit{digit}.

decimal = digit{digit}'.'digit{digit}.

float = digit{digit}'.'digit{digit}('E' | 'e')digit{digit}.

id = letter{letter | digit | '_' | '#'}.

</pre>

<p>$Id$

</html>
