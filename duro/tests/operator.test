#!/bin/sh
# Execute tclsh from the user's PATH \
exec tclsh "$0" ${1+"$@"}

# $Id$
#
# Test creating, invoking, deleting operators
#

package require tcltest 2.2
eval ::tcltest::configure $argv
namespace import ::tcltest::*

source [file dirname [info script]]/setup.tcl

test ro_operator {read-only operators} -setup $SETUP -body {
    set dbenv [duro::env create [configure -tmpdir]/dbenv]

    # Create Database
    duro::db create $dbenv TEST
    
    # Start transaction
    set tx [duro::begin $dbenv TEST]
    
    # Create read-only operator
    duro::operator create concat -returns string {a string b string} {
        return $a$b
    } $tx

    # Create read-only operator with relation argument and return value
    duro::operator create relop1 -returns {relation {A string}} \
            {r {relation {A string}}} {
        lappend r {A b}
        return $r
    } $tx

    # Create read-only operator with tuple argument
    duro::operator create TSWAP -returns {tuple {A string} {B string}} \
        {r {tuple {A string} {B string}}} {
        array set a $r
        return [list A $a(B) B $a(A)]
    } $tx
    
    # Create read-only operator w/o arguments
    duro::operator create zero -returns integer {} {
        return 0
    } $tx

    if {![tequal [duro::expr {TSWAP (TUPLE {A "a", B "b"})} $tx] {A b B a}]} {
        error "unexpected value of TSWAP()"
    }
    
    duro::commit $tx
    
    # Close DB environment
    duro::env close $dbenv
    
    # Reopen DB environment
    set dbenv [duro::env open [configure -tmpdir]/dbenv]
    
    set tx [duro::begin $dbenv TEST]
    
    set v [duro::expr {concat("X", "Y")} $tx]
    if {![string equal $v XY]} {
       error "result is %s, should be %s" $v XY
    }
    
    set v [duro::expr {zero()} $tx]
    if {$v != 0} {
        error "result of zero is $v"
    }
    
    # Delete operator
    duro::operator drop concat $tx
    
    set r [duro::expr {relop1(RELATION {TUPLE {A "a"}})} $tx]
    set sr {{A a} {A b}}
    if {[lsort $r] != $sr} {
        error "relation should be $sr, but is $r"
    }
    
    duro::commit $tx

    duro::env close $dbenv
}

test upd_operator {update operators} -setup $SETUP -body {
    set dbenv [duro::env create [configure -tmpdir]/dbenv]

    # Create Database
    duro::db create $dbenv TEST
    
    # Start transaction
    set tx [duro::begin $dbenv TEST]

    # Create overloaded update operator
    
    duro::operator create strmul -updates {a} {a string b string} {
        upvar $a la
    
        append la $b
    } $tx
    
    duro::operator create strmul -updates {a} {a string b integer} {
        upvar $a la
    
        set h $la
        for {set i 1} {$i < $b} {incr i} {
            append la $h
        }
    } $tx

    # Create update operator with relation argument

	duro::operator create TUP0 -updates {t} {t {tuple {I integer}}} {
    	upvar $t tl
	    set tl {I 0}
	} $tx

    # Create update operator with relation argument
    duro::operator create relop2 -updates {r1} {r1 {relation {A string}} \
            r2 {relation {A string}}} {
        foreach i $r2 {
            duro::insert $r1 $i $tx
        }
    } $tx

	# Test TUP0

    set t {I 1}

    duro::call TUP0 t {tuple {I integer}} $tx

    puts $t
    
    duro::table create -local t {
       {A string}
    } {{A}} $tx
    
    duro::insert t {A a} $tx
    
    duro::call relop2 t {relation {A string}} {{A b}} {relation {A string}} $tx
    
    set arr [duro::array create t {A asc} $tx]
    checkarray $arr {{A a} {A b}} $tx
    duro::array drop $arr

    duro::commit $tx

    # Close DB environment
    duro::env close $dbenv
    
    # Reopen DB environment
    set dbenv [duro::env open [configure -tmpdir]/dbenv]
    
    set tx [duro::begin $dbenv TEST]

    # Invoke update operator
    set v foo
    duro::call strmul v string bar string $tx
    
    if {![string equal $v foobar]} {
        error [format "result is %s, should be %s" $v foobar]
    }
    
    set i X
    set v foo
    duro::call strmul v string 3 integer $tx
    
    if {![string equal $v foofoofoo]} {
        error [format "result is %s, should be %s" $v foofoofoo]
    }
    
    if {$i != "X"} {
        error "global variable was modified by operator"
    }
    
    duro::operator drop strmul $tx

    # Try to invoke deleted operator

    if {![catch {
        duro::call strmul v string bar string $tx
    } err]} {
        error "Operator invocation should fail, but succeded"
    }
    set errcode [lindex $::errorCode 1]
    if {![string match "operator_not_found_error(*)" $errcode]} {
        error "Wrong error code: $errcode"
    }

    duro::table create -local t {
       {A string}
    } {{A}} $tx

    duro::call relop2 t {relation {A string}} {{A b} {A c}} {relation {A string}} \
            $tx
    
    set arr [duro::array create t {A asc} $tx]
    checkarray $arr {{A b} {A c}} $tx
    duro::array drop $arr
    
    duro::table drop t $tx

    duro::commit $tx

    duro::env close $dbenv
} -output {I 0
}

cleanupTests
